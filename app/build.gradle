apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "com.ballastlane.android.texasam"
        minSdkVersion 17
        targetSdkVersion 27
        versionCode 1
        versionName "0.0.1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }

    dataBinding {
        enabled = true
    }

    signingConfigs {

        qa {
            try {
                storeFile file("keystore/debug.keystore")
                storePassword "android"
                keyAlias "androiddebugkey"
                keyPassword "android"
            } catch (Throwable e) {
            }
        }

        release {
            try {
                def keystorePropertiesFile = rootProject.file("local.properties")
                def keystoreProperties = new Properties()
                keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
                storeFile file("keystore/production.keystore")
                storePassword keystoreProperties['productionKeyPassword']
                keyAlias keystoreProperties['productionAlias']
                keyPassword keystoreProperties['productionAliasPassword']
            } catch (Throwable e) {
            }
        }
    }
    buildTypes {

        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '.debug'
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }

        qa {
            minifyEnabled false
            applicationIdSuffix '.qa'
            versionNameSuffix '.qa'
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.qa
        }

    }
    applicationVariants.all { variant ->
        def flavor = variant.mergedFlavor
        def versionCode = flavor.versionCode
        if (variant.buildType.name == 'qa' && properties['BUILD_NUMBER'] != null) {
            versionCode = properties['BUILD_NUMBER'] as Integer
        }
        flavor.versionCode = versionCode
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
        exclude "META-INF/ASL2.0"
    }
    dexOptions {
        javaMaxHeapSize "1g"
    }
    lintOptions {
        abortOnError false        // true by default
        checkAllWarnings false
        checkReleaseBuilds false
        ignoreWarnings true       // false by default
        quiet true                // false by default
    }
    buildToolsVersion '27.0.3'
}
kapt {
    generateStubs = true
    correctErrorTypes = true
}
dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // ---------- Android Base Library
    implementation project(':baselibrary')

    // ---------- Android support
    implementation 'com.android.support:appcompat-v7:27.1.0'
    implementation 'com.android.support:support-v4:27.1.0'
    implementation 'com.android.support:recyclerview-v7:27.1.0'
    implementation 'com.android.support:cardview-v7:27.1.0'
    implementation "com.android.support:design:27.1.0"
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation 'com.android.support:customtabs:27.1.0'

    // ---------- Databinding
    kapt 'com.android.databinding:compiler:3.1.0'

    // ---------- Kotlin
    implementation 'org.jetbrains.kotlin:kotlin-stdlib:1.2.31'
    implementation 'org.jetbrains.anko:anko-common:0.9'

    // ---------- Dagger
    implementation "com.google.dagger:dagger:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"

    // ---------- OkHttp
    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"

    // ---------- Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofit_version"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'

    // ---------- Rx
    implementation "io.reactivex.rxjava2:rxjava:$rxjava_version"
    implementation "io.reactivex.rxjava2:rxandroid:$rxandroid_version"
    implementation "com.jakewharton.rxbinding2:rxbinding-kotlin:$rxbinding_version"
    implementation "com.tbruyelle.rxpermissions2:rxpermissions:$rxpermissions_version"
    implementation 'com.jakewharton.timber:timber:4.6.0'

    // ---------- Font
    implementation "uk.co.chrisjenx:calligraphy:$calligraphy_version"

    // ---------- Eventbus
    implementation 'org.greenrobot:eventbus:3.0.0'

    // ---------- Gson
    implementation 'com.google.code.gson:gson:2.8.2'

    // ---------- For Images
    implementation 'com.soundcloud.android:android-crop:1.0.1@aar'
    implementation 'de.hdodenhof:circleimageview:2.1.0'
    implementation('com.github.bumptech.glide:glide:4.7.1') {
        exclude group: "com.android.support"
    }
    annotationProcessor 'com.github.bumptech.glide:compiler:4.6.1'
    implementation 'com.github.bumptech.glide:okhttp3-integration:4.6.1'

    // ---------- Joda-time
    implementation "joda-time:joda-time:$joda_time_version"

    // ----------  ViewModel and LiveData
    implementation "android.arch.lifecycle:extensions:$lifecycle_version"
    annotationProcessor "android.arch.lifecycle:compiler:$lifecycle_version"

    // ----------  Paging
    implementation 'android.arch.paging:runtime:1.0.0-alpha7'

    // ----------  ReactiveStreams support for LiveData
    implementation 'android.arch.lifecycle:reactivestreams:1.1.1'
}

repositories {
    mavenCentral()
}